import artefactos.*
import personaje.*
import hechizo.*
import LibroDeHechizos.*

describe "Test de lucha mas avanzada" {

	const espadaDelDestino = new ArmaDeFilo()
	const mascaraOscura = new Mascara(indiceDeOscuridad = 1)
	const collarDivino = new CollarDivino()
	const armadura = new Armadura()
	var rolando = new Personaje(
		hechizoPreferido = new Logos(nombre = "espectro malefico"),
		artefactos = [ espadaDelDestino, collarDivino, mascaraOscura, armadura, espejoFantastico ]
	)

	test "1) Cuando Rolando tiene 5 ó mas artefactos esta cargado" {
		assert.that(rolando.estasCargado())
	}

	test "2) Cuando Rolando se saca la armadura, queda con menos de 4 artefactos, por lo tanto no esta cargado" {
		rolando.eliminaUnArtefacto(armadura)
		assert.notThat(rolando.estasCargado())
	}

	test "3) Rolando tiene una habilidad de lucha igual a 20" {
		assert.equals(rolando.habilidadDeLucha(), 20)
	}

	test "4) Cuando la armadura de Rolando tiene un refuerzo de bendición su poder de lucha es de 58" {
		armadura.refuerzo(bendicion)
		assert.equals(armadura.poderDeLucha(rolando), 58)
	}

	test "5) Cuando la armadura de Rolando tiene un refuerzo de cota de malla su poder de lucha es de 3" {
		armadura.refuerzo(new CotaDeMalla(unidadesDeLucha = 1))
		assert.equals(armadura.poderDeLucha(rolando), 3)
	}

	test "6) Cuando la armadura de Rolando tiene un refuerzo de hechizo común su poder de lucha es de 12" {
		armadura.refuerzo(hechizoBasico)
		assert.equals(armadura.poderDeLucha(rolando), 12)
	}

	test "7) Cuando la armadura de Rolando tiene un refuerzo de espectro maléfico su poder de lucha es de 19" {
		armadura.refuerzo(new Hechizo(nombre = "espectro malefico"))
		assert.equals(armadura.poderDeLucha(rolando), 19)
	}

	test "8) Cuando Rolando solo tiene el espejo entre sus artefactos su habilidad de lucha es de 1" {
		rolando.eliminaTodosLosArtefactos()
		rolando.agregaUnArtefacto(espejoFantastico)
		assert.equals(rolando.habilidadDeLucha(), 1)
	}

	test "9) Con un libro de hechizos con el hechizo básico y el espectro maléfico Rolando tiene un nivel de hechizeria de 56" {
		const espectroMalefico = new Logos(nombre = "espectro maléfico")
		const libroDeHechizos = new LibroDeHechizos(hechizos = [espectroMalefico, hechizoBasico])
		rolando.hechizoPreferido(libroDeHechizos)
		assert.equals(rolando.nivelDeHechiceria(), 56)
	}

	test "10) Teniendo rolando una armadura extra con un valor base de 5 y una cota de malla de refuerzo, su habilidad de lucha es de 37" {
		const armaduraExtra = new Armadura()
		armaduraExtra.valorBase(5)
		armaduraExtra.refuerzo(new CotaDeMalla(unidadesDeLucha = 6))
		rolando.agregaUnArtefacto(armaduraExtra)
		assert.equals(rolando.habilidadDeLucha(), 37)
	}

}

